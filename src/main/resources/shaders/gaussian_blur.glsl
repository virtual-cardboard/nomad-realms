#version 330 core
out vec4 fragColor;

in vec2 texCoords;

uniform sampler2D screenTexture;
uniform int horizontal;
uniform float radius;

float weight[5] = float[] (0.227027, 0.1945946, 0.1216216, 0.05405405, 0.016216216);

void main() {
    vec2 tex_offset = 1.0 / textureSize(screenTexture, 0); // gets size of single texel
    vec3 result = texture(screenTexture, texCoords).rgb * weight[0]; // current fragment's contribution
    if (horizontal == 1) {
        for (int i = 1; i < 5; ++i) {
            result += texture(screenTexture, texCoords + vec2(tex_offset.x * i * radius, 0.0)).rgb * weight[i];
            result += texture(screenTexture, texCoords - vec2(tex_offset.x * i * radius, 0.0)).rgb * weight[i];
        }
    } else {
        for (int i = 1; i < 5; ++i) {
            result += texture(screenTexture, texCoords + vec2(0.0, tex_offset.y * i * radius)).rgb * weight[i];
            result += texture(screenTexture, texCoords - vec2(0.0, tex_offset.y * i * radius)).rgb * weight[i];
        }
    }
    fragColor = vec4(result, 1.0);
}
